  100 '     save "tree.bas",a
  110 '     save "tree.bas",a
  120 LEVEL=15
  130 LONG=80
  140 X=300
  150 Y=350
  160 PI=3.14159
  170 ROTATION=PI/2
  180 A1=PI/9 : A2=PI/9
  190 C1=0.75 : C2=0.75
  200 DIM XX(LEVEL):DIM YY(LEVEL)
  210 CLS 3
  220 GOSUB 240
  230 END
  240 REM
  250 XX(LEVEL)=X:YY(LEVEL)=Y
  260 GOSUB 400
  270 IF LEVEL=1 THEN GOTO 380
  280 LEVEL=LEVEL-1
  290 LONG=LONG*C1
  300 ROTATION=ROTATION-A1
  310 GOSUB 240
  320 LONG=LONG/C1*C2
  330 ROTATION=ROTATION+A1+A2
  340 GOSUB 240
  350 ROTATION=ROTATION-A2
  360 LONG=LONG/C2
  370 LEVEL=LEVEL+1
  380 X=XX(LEVEL):Y=YY(LEVEL)
  390 RETURN
  400 REM
  410 YN=-SIN(ROTATION)*LONG+Y
  420 XN=COS(ROTATION)*LONG+X
  430 IF LEVEL=1 THEN COL=3 ELSE COL=4
  440 LINE(X,Y)-(XN,YN),COL
  450 X=XN:Y=YN
  460 RETURN
