  100 '     save "tree.bas",a
  110 LEVEL=15
  120 LONG=80
  130 X=300
  140 Y=350
  150 PI=3.14159
  160 ROTATION=PI/2
  170 A1=PI/9 : A2=PI/9
  180 C1=0.75 : C2=0.75
  190 DIM XX(LEVEL):DIM YY(LEVEL)
  200 CLS 3
  210 GOSUB 230
  220 END
  230 REM
  240 XX(LEVEL)=X:YY(LEVEL)=Y
  250 GOSUB 390
  260 IF LEVEL=1 THEN GOTO 370
  270 LEVEL=LEVEL-1
  280 LONG=LONG*C1
  290 ROTATION=ROTATION-A1
  300 GOSUB 230
  310 LONG=LONG/C1*C2
  320 ROTATION=ROTATION+A1+A2
  330 GOSUB 230
  340 ROTATION=ROTATION-A2
  350 LONG=LONG/C2
  360 LEVEL=LEVEL+1
  370 X=XX(LEVEL):Y=YY(LEVEL)
  380 RETURN
  390 REM
  400 YN=-SIN(ROTATION)*LONG+Y
  410 XN=COS(ROTATION)*LONG+X
  420 IF LEVEL=1 THEN COL=3 ELSE COL=4
  430 LINE(X,Y)-(XN,YN),COL
  440 X=XN:Y=YN
  450 RETURN
